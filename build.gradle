buildscript {
    ext {
        springBootVersion = '2.3.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}
apply plugin: 'idea'

allprojects {
    apply plugin: 'java'

    group = 'jp.ac.kagawalab'
    version = '0.0.1'
//noinspection GroovyUnusedAssignment
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    test {
        useJUnitPlatform()
    }
    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    /** spring boot周りの依存関係を解決する
     * ref: https://qiita.com/opengl-8080/items/6409669dc3f8b8071af4
     */
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }
}

project(':web') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        implementation project(':core')

        // spring boot
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.session:spring-session-core'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        runtimeOnly 'org.springframework.boot:spring-boot-devtools'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'org.springframework.security:spring-security-test'

        runtimeOnly 'org.postgresql:postgresql'

        // thymeleaf
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
        implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
        implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.3.0'

        // others
        implementation 'org.modelmapper:modelmapper:latest.release'
    }
}

project(':core') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.security:spring-security-oauth2-core'
        implementation 'org.modelmapper:modelmapper:latest.release'
//        implementation 'org.glassfish:javax.el:latest.release'
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }
}

